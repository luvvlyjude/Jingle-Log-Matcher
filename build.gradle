plugins {
    id 'java'
    id 'idea'
}

group = project.maven_group
version = project.plugin_version
//noinspection GroovyUnusedAssignment
archivesBaseName = project.archives_base_name

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
}

compileJava {
    options.compilerArgs << '-parameters'
}

configurations {
    provided
    implementation.extendsFrom(provided)
}

// Ensures latest Jingle commit
configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    // Choose Jingle version: https://jitpack.io/#DuncanRuns/Jingle/
    implementation 'com.github.DuncanRuns:Jingle:main-SNAPSHOT'
}

processResources {
    // Totally stolen from Fabric
    inputs.property "version", project.version

    filesMatching("jingle.plugin.json") {
        expand "version": project.version
    }
}

jar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    from { configurations.provided.asFileTree.files.collect { zipTree(it) } } {
        include '**/*.class'
        exclude '**/module-info.*'
    }
}

var jinglePluginsFolderPath = "${System.getProperty('user.home')}/.config/Jingle/plugins"
tasks.register('copyJar', Copy) {
    from jar
    into jinglePluginsFolderPath
}